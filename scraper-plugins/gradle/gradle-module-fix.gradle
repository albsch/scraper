// this is necessary until gradle provides first-class support for java modules

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

compileTestJava {
    final String moduleName = project.name.toString().replace("-", ".")
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'junit,scraper.nodes.test',
                '--add-reads', "$moduleName=junit,scraper.nodes.test",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}

test {
    final String moduleName = project.name.toString().replace("-", ".")
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'ALL-MODULE-PATH,scraper.nodes.test',
                '--add-reads',"$moduleName=junit,scraper.nodes.test",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
        ]
        classpath = files()
    }
    testLogging {
        exceptionFormat = 'full'
    }
}
